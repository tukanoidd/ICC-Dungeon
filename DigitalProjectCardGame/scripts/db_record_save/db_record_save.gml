/// @desc db_record_save(record)
/// @arg {ds_map} record - a ds_map representation of a record

/*
	Saves the values of a record as defined by its ds_map representation (as generated by the record functions).
	If the record is new, it gets created. In case of an existing record, the values are updated.
*/

var _record = argument0;
var _table_name = _record[? "_table_name"];
var _table = db_table_load(_table_name);
var _table_column_names = _table[? "column_names"];

//generate values array
var _values = [];
var _values_length = 0;
var _column_count = ds_list_size(_table_column_names);
for(var _i = 1; _i < _column_count; _i++) {
	var _column_name = _table_column_names[| _i];
	if(ds_map_exists(_record,_column_name)) {
		var _value = _record[? _column_name];
		_values[_values_length++] = [_column_name,_value];
	}
}

//record exists, update
if(ds_map_exists(_record,"id")) {
	var _id = _record[? "id"];
	db_record_update(_table,_id,_values);
}
else {
	var _id = db_record_create(_table,_values);
	_record[? "id"] = _id;
}